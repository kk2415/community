buildscript {
    ext {
        springBootVersion = '2.6.8'
        snippetDir = file('build/generated-snippets')
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.5.3"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "org.asciidoctor.jvm.convert"
    apply plugin: "me.champeau.gradle.jmh"
    apply plugin: "java-test-fixtures"

    group = 'com.levelup'
    version = 'v1.0'
    sourceCompatibility = '11'

    configurations {
        all {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        set('springCloudVersion', "2021.0.2")
        snippetDir = file('build/generated-snippets')
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        implementation 'mysql:mysql-connector-java'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-cache'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        implementation 'com.fasterxml.jackson.core:jackson-core:2.13.4'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation("org.springframework.boot:spring-boot-starter-oauth2-client")

        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'

        implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

        implementation group: 'org.jsoup', name: 'jsoup', version: '1.15.2'
        jmhImplementation 'org.openjdk.jmh:jmh-core:1.35'
        jmhImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.35'

        implementation 'org.springframework.boot:spring-boot-starter-log4j2'

        runtimeOnly 'com.h2database:h2'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'org.mockito:mockito-inline:3.6.0'
    }
 
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        outputs.dir(snippetDir)
        useJUnitPlatform()
    }

    asciidoctor {
        inputs.dir(snippetDir)
        dependsOn test
    }

    jmh {
        fork = 1
        warmupIterations = 1
        iterations = 1
    }
}